# Project: adams
# Date: 2017-12-20 13:51:18
# User: fracpete
# Charset: UTF-8
adams.flow.control.Flow -annotation "Generates compatible ARFF files from several spreadsheets." -flow-execution-listener adams.flow.execution.NullListener
 adams.flow.source.Start
 adams.flow.control.Trigger -name "load files"
  adams.flow.source.WekaSelectDataset -output-array true -stop-if-canceled true -file-chooser-title "Select datasets" -initial-dir ${FLOWS}/data
  adams.flow.control.Tee -name "num files"
   adams.flow.transformer.ArrayLength
   adams.flow.transformer.SetVariable -var-name num_files
  adams.flow.control.Trigger -name "new length array"
   adams.flow.source.NewArray -array-class java.lang.Integer -dimensions [@{num_files}]
   adams.flow.transformer.SetStorageValue -storage-name lengths
  adams.flow.control.Trigger -name "new name array"
   adams.flow.source.NewArray -dimensions [@{num_files}]
   adams.flow.transformer.SetStorageValue -storage-name names
  adams.flow.transformer.SetVariable -var-name index -var-value 0
  adams.flow.control.ArrayProcess
   adams.flow.transformer.IncVariable -var-name index
   adams.flow.control.Tee -name name
    adams.flow.transformer.BaseName -remove-extensions true
    adams.flow.transformer.SetVariable -var-name name
    adams.flow.control.Trigger -name "update array"
     adams.flow.source.StorageValue -storage-name names -conversion adams.data.conversion.UnknownToUnknown
     adams.flow.transformer.SetArrayElement -index @{index} -value @{name}
     adams.flow.transformer.SetStorageValue -storage-name names
   adams.flow.transformer.WekaFileReader -loader weka.core.converters.AArffLoader
   adams.flow.transformer.Convert -conversion "adams.data.conversion.WekaInstancesToSpreadSheet -data-row-type adams.data.spreadsheet.DenseDataRow -spreadsheet-type adams.data.spreadsheet.DefaultSpreadSheet"
   adams.flow.control.Tee -name "num rows"
    adams.flow.transformer.SpreadSheetInfo
    adams.flow.transformer.SetVariable -var-name length
    adams.flow.control.Trigger -name "update array"
     adams.flow.source.StorageValue -storage-name lengths -conversion adams.data.conversion.UnknownToUnknown
     adams.flow.transformer.SetArrayElement -index @{index} -value @{length}
     adams.flow.transformer.SetStorageValue -storage-name lengths
  adams.flow.transformer.SetStorageValue -storage-name sheets
 adams.flow.control.Trigger -name "select output dir"
  adams.flow.source.SelectDirectory -stop-if-canceled true -dir-chooser-title "Select output dir" -initial-dir ${FLOWS}/output
  adams.flow.transformer.SetVariable -var-name outdir
 adams.flow.control.Trigger -name "combine files"
  adams.flow.source.StorageValue -storage-name sheets -conversion adams.data.conversion.UnknownToUnknown
  adams.flow.transformer.ArrayToSequence
  adams.flow.transformer.SpreadSheetAppend -no-copy true -storage-name combined_sheets
 adams.flow.control.Trigger -name "create combined arff"
  adams.flow.source.StorageValue -storage-name combined_sheets -conversion adams.data.conversion.UnknownToUnknown
  adams.flow.transformer.Convert -conversion adams.data.conversion.SpreadSheetToWekaInstances
  adams.flow.transformer.SetStorageValue -storage-name combined_arff
 adams.flow.control.Trigger -name "create separate arff"
  adams.flow.source.StorageForLoop -upper @{num_files} -variable index -storage combined_arff
  adams.flow.control.Trigger -name "get name"
   adams.flow.source.StorageValue -storage-name names -conversion adams.data.conversion.UnknownToUnknown
   adams.flow.transformer.GetArrayElement -index @{index}
   adams.flow.transformer.SetVariable -var-name name
  adams.flow.control.Trigger -name "get length"
   adams.flow.source.StorageValue -storage-name lengths -conversion adams.data.conversion.UnknownToUnknown
   adams.flow.transformer.GetArrayElement -index @{index}
   adams.flow.transformer.SetVariable -var-name length
  adams.flow.control.Trigger -name "create range"
   adams.flow.source.CombineVariables -expression 1-@{length} -conversion adams.data.conversion.StringToString
   adams.flow.transformer.SetVariable -var-name range
  adams.flow.control.Trigger -name "output file"
   adams.flow.source.CombineVariables -expression @{name}.arff -conversion adams.data.conversion.StringToString
   adams.flow.transformer.PrependDir -prefix @{outdir}
   adams.flow.transformer.SetVariable -var-name outfile
  adams.flow.control.Tee -name "save file"
   adams.flow.control.UpdateProperties -property filter.instancesIndices -variable range
    adams.flow.transformer.WekaFilter -filter "weka.filters.unsupervised.instance.RemoveRange -V -R first-last"
   adams.flow.transformer.WekaRenameRelation -replace @{name}
   adams.flow.sink.WekaFileWriter -output @{outfile} -saver "weka.core.converters.ArffSaver -decimal 6"
  adams.flow.control.SubProcess -name "update dataset"
   adams.flow.control.UpdateProperties -property filter.instancesIndices -variable range
    adams.flow.transformer.WekaFilter -filter "weka.filters.unsupervised.instance.RemoveRange -R first-last"
   adams.flow.transformer.SetStorageValue -storage-name combined_arff
# Project: adams
# Date: 2016-02-15 15:25:49
# User: fracpete
# Charset: UTF-8
adams.flow.control.Flow -annotation "Allows the user to generate individual plots from a matrix plot,\\nplotting two sets of attributes (selected by the user) against\\neach other." -flow-execution-listener adams.flow.execution.NullListener
 adams.flow.standalone.CallableActors
  adams.flow.sink.DisplayPanelManager -width 900 -height 600 -provider "adams.flow.sink.WekaInstancesPlot -writer adams.gui.print.NullWriter" -entry-name-variable label
 adams.flow.source.WekaSelectDataset -stop-if-canceled true -initial-file ${FLOWS}/data/bodyfat.arff
 adams.flow.transformer.WekaFileReader -loader weka.core.converters.AArffLoader
 adams.flow.transformer.SetStorageValue -storage-name data
 adams.flow.transformer.WekaFilter -filter "weka.filters.unsupervised.instance.RemoveRange -R first-last"
 adams.flow.transformer.SetStorageValue -name SetStorageValue-1 -storage-name atts
 adams.flow.control.Trigger -name "first lot of attributes"
  adams.flow.source.StorageValue -storage-name atts
  adams.flow.transformer.WekaChooseAttributes -stop-if-canceled true -message "Choose the first set of attributes"
  adams.flow.transformer.WekaInstancesInfo -output-array true -type ATTRIBUTE_NAMES
  adams.flow.transformer.Convert -conversion adams.data.conversion.JoinOptions
  adams.flow.transformer.SetVariable -var-name first_atts
 adams.flow.control.Trigger -name "second lot of attributes"
  adams.flow.source.StorageValue -storage-name atts
  adams.flow.transformer.WekaChooseAttributes -stop-if-canceled true -message "Choose the second set of attributes to plot against"
  adams.flow.transformer.WekaInstancesInfo -output-array true -type ATTRIBUTE_NAMES
  adams.flow.transformer.Convert -conversion adams.data.conversion.JoinOptions
  adams.flow.transformer.SetVariable -var-name second_atts
 adams.flow.control.Trigger -name "iterate 1st lot"
  adams.flow.source.StringConstants -string @{first_atts}
  adams.flow.transformer.SetVariable -var-name first_att
  adams.flow.control.Trigger -name "iterate 2nd lot"
   adams.flow.source.StringConstants -string @{second_atts}
   adams.flow.transformer.SetVariable -var-name second_att
   adams.flow.control.Trigger -name regexp
    adams.flow.source.CombineVariables -expression (@{first_att}|@{second_att})
    adams.flow.transformer.SetVariable -var-name current
   adams.flow.control.Trigger -name label
    adams.flow.source.CombineVariables -expression "@{first_att} vs @{second_att}"
    adams.flow.transformer.SetVariable -var-name label
   adams.flow.control.Trigger -name "create plot"
    adams.flow.source.StorageValue -storage-name data
    adams.flow.control.UpdateProperties -property filter.expression -variable current -sub-actor adams.flow.transformer.PassThrough
     adams.flow.transformer.WekaFilter -filter "weka.filters.unsupervised.attribute.RemoveByName -E ^.*id$ -V" -keep true
    adams.flow.sink.CallableSink -callable DisplayPanelManager
